RANDOMIZE TIMER
SCREEN 9
PRINT "                              das Duell der"
PRINT "                               P A N Z E R"
PRINT "                                von Rafael"
PRINT : PRINT : PRINT
PRINT "Anleitung (j, n)?"
DO: t$ = INKEY$: LOOP UNTIL t$ = "j" OR t$ = "n"
IF t$ = "j" THEN
 CLS
 LOCATE 3, 26: PRINT "S P I E L A N L E I T U N G"
 PRINT : PRINT : PRINT
 PRINT "             Spieler 1                                 Spieler 2"
 PRINT
 PRINT "            (Schneller)                               (Schneller)"
 PRINT "                 W                                         " + CHR$(24)
 PRINT "       (Links) A   D (Rechts)                    (Links) " + CHR$(27) + "   " + CHR$(26) + " (Rechts)"
 PRINT "                 S                                         " + CHR$(25)
 PRINT "            (Langsamer)                               (Langsamer)"
 PRINT
 PRINT "             Schuss: 1                               Schuss: Enter"
 DO: LOOP WHILE INKEY$ = ""
END IF
DO
INPUT "Wieviele Leben hat Spieler 1"; l1
LOOP UNTIL l1 > 0 AND l1 < 20
DO
INPUT "Wieviele Leben hat Spieler 2"; l2
LOOP UNTIL l2 > 0 AND l2 < 20
SCREEN 7, , 0, 1
DIM a(40, 3, 1)
'Malen & einlesen
FOR y = 0 TO 31
 READ a$
 FOR x = 0 TO LEN(a$) - 1
  PSET (x, y), ASC(MID$(a$, x + 1, 1)) - 65
 NEXT x
NEXT y
FOR x = 0 TO 3
 FOR y = 0 TO 1
  GET (x * 16, y * 16)-STEP(15, 15), a(0, x, y)
 NEXT y
NEXT x
CLS

'Koordinaten      Richtung  Speed   Leben
x1 = 50: y1 = 150: r1 = 0: g1 = 0
x2 = 250: y2 = 50: r2 = 2: g2 = 0
DO
 t$ = INKEY$
 pp = (pp + 1) MOD 2
 SELECT CASE t$
 CASE "w": IF g1 < 10 THEN g1 = g1 + 1
 CASE "s": IF g1 >= 0 THEN g1 = g1 - 1
 CASE "a": r1 = (r1 + 1) MOD 4
 CASE "d": r1 = (r1 + 3) MOD 4
 CASE "1": shoter = 1: GOSUB shot
 CASE CHR$(0) + "H": IF g2 < 10 THEN g2 = g2 + 1
 CASE CHR$(0) + "P": IF g2 >= 0 THEN g2 = g2 - 1
 CASE CHR$(0) + "K": r2 = (r2 + 1) MOD 4
 CASE CHR$(0) + "M": r2 = (r2 + 3) MOD 4
 CASE CHR$(13): shoter = 2: GOSUB shot
 CASE CHR$(27): END
 CASE ELSE
 END SELECT
 SELECT CASE r1
 CASE 0: x1 = x1 + (1 * g1)
 CASE 1: y1 = y1 - (1 * g1)
 CASE 2: x1 = x1 - (1 * g1)
 CASE 3: y1 = y1 + (1 * g1)
 CASE ELSE
 END SELECT
 SELECT CASE r2
 CASE 0: x2 = x2 + (1 * g2)
 CASE 1: y2 = y2 - (1 * g2)
 CASE 2: x2 = x2 - (1 * g2)
 CASE 3: y2 = y2 + (1 * g2)
 CASE ELSE
 END SELECT
 IF x1 < 1 THEN x1 = 1
 IF x1 > 300 THEN x1 = 300
 IF y1 < 10 THEN y1 = 10
 IF y1 > 180 THEN y1 = 180
 IF x2 < 1 THEN x2 = 1
 IF x2 > 300 THEN x2 = 300
 IF y2 < 10 THEN y2 = 10
 IF y2 > 180 THEN y2 = 180
 CLS
 COLOR 1
 PRINT "Spieler 1:"; l1; "Leben   Spieler 2:"; l2; "Leben"
 PUT (x1, y1), a(0, r1, pp), PSET
 PUT (x2, y2), a(0, r2, pp), PSET
 FOR khf = 1 TO 10000: NEXT khf
 PCOPY 0, 1
LOOP
END

shot:
drop = 0
IF shoter = 1 THEN ri = r1 ELSE ri = r2
SELECT CASE ri
CASE 0:
 IF shoter = 1 THEN
  n = x1 + 16: m = y1 + 7
 ELSE
  n = x2 + 16: m = y2 + 7
 END IF
 DO
  IF POINT(n, m) <> 0 OR POINT(n, m + 1) <> 0 THEN
   CIRCLE (n, m), 10, 4
   PAINT (n, m), 4, 4
   PCOPY 0, 1
   drop = 1
   EXIT DO
  ELSE
   LINE (n, m)-STEP(0, 1), 15
  END IF
  n = n + 1
 LOOP UNTIL n > 315
 PCOPY 0, 1
CASE 1:
 IF shoter = 1 THEN
  n = x1 + 7: m = y1 - 1
 ELSE
  n = x2 + 7: m = y2 - 1
 END IF
 DO
  IF POINT(n, m) <> 0 OR POINT(n + 1, m) <> 0 THEN
   CIRCLE (n, m), 10, 4
   PAINT (n, m), 4, 4
   PCOPY 0, 1
   drop = 1
   EXIT DO
  ELSE
   LINE (n, m)-STEP(1, 0), 15
  END IF
  m = m - 1
 LOOP UNTIL m < 10
 PCOPY 0, 1
CASE 2:
 IF shoter = 1 THEN
  n = x1 - 1: m = y1 + 7
 ELSE
  n = x2 - 1: m = y2 + 7
 END IF
 DO
  IF POINT(n, m) <> 0 OR POINT(n, m + 1) <> 0 THEN
   CIRCLE (n, m), 10, 4
   PAINT (n, m), 4, 4
   PCOPY 0, 1
   drop = 1
   EXIT DO
  ELSE
   LINE (n, m)-STEP(0, 1), 15
  END IF
  n = n - 1
 LOOP UNTIL n < 1
 PCOPY 0, 1
CASE 3:
 IF shoter = 1 THEN
  n = x1 + 7: m = y1 + 16
 ELSE
  n = x2 + 7: m = y2 + 16
 END IF
 DO
  IF POINT(n, m) <> 0 OR POINT(n + 1, m) <> 0 THEN
   CIRCLE (n, m), 10, 4
   PAINT (n, m), 4, 4
   PCOPY 0, 1
   drop = 1
   EXIT DO
  ELSE
   LINE (n, m)-STEP(1, 0), 15
  END IF
  m = m + 1
 LOOP UNTIL m > 180
 PCOPY 0, 1
 CASE ELSE
END SELECT
IF drop = 1 THEN
 IF shoter = 1 THEN
  x2 = INT(RND * 298 + 2)
  y2 = INT(RND * 169 + 11)
  r2 = INT(RND * 4)
  l2 = l2 - 1
  IF l2 = 0 THEN winner = 1: GOSUB win
 ELSE
  x1 = INT(RND * 298 + 2)
  y1 = INT(RND * 169 + 11)
  r1 = INT(RND * 4)
  l1 = l1 - 1
  IF l1 = 0 THEN winner = 2: GOSUB win
 END IF
END IF
RETURN

win:
LOCATE 1, 1
COLOR 1
PRINT "Spieler 1:"; l1; "Leben   Spieler 2:"; l2; "Leben"
LOCATE 12, 9
COLOR 12
PRINT "Spieler"; winner; "hat Gewonnen!"
PCOPY 0, 1
FOR x = 1 TO 100: q$ = INKEY$: NEXT x
DO: LOOP UNTIL INKEY$ = CHR$(27)
END


DATA CCCCCCCCCCCCAAAAAAAAAAACCAAAAAAAAAAACCCCCCCCCCCCCCCCAAAAAAAACCCC
DATA CKCCCCKCCCKCAAAAAAAAAAACCAAAAAAAAAAACKCCCKCCCCKCCKKCAKKKKKKACKKC
DATA CKCCCCKCCCKCAAAAAAAAAAACCAAAAAAAAAAACKCCCKCCCCKCCCCCKKKKKKKKCCCC
DATA CCCCCCCCCCCCAAAAAAAAAAACCAAAAAAAAAAACCCCCCCCCCCCCCCCKKKCCKKKCCCC
DATA AAKKKKKKKAAAAAAACCCCAAACCAAACCCCAAAAAAAKKKKKKKAACCCCKKCCCCKKCCCC
DATA AKKKKKKKKKAAAAAACKKCAAACCAAACKKCAAAAAAKKKKKKKKKACCCCKKCCCCKKCCCC
DATA AKKKCCCKKKAAAAAACCCCAKKCCKKACCCCAAAAAAKKKCCCKKKACKKCKKCCCCKKCKKC
DATA AKKCCCCCCCCCCCCCCCCCKKKCCKKKCCCCCCCCCCCCCCCCCKKACCCCKKKCCKKKCCCC
DATA AKKCCCCCCCCCCCCCCCCCKKKCCKKKCCCCCCCCCCCCCCCCCKKACCCCKKKCCKKKCCCC
DATA AKKKCCCKKKAAAAAACKKCKKCCCCKKCKKCAAAAAAKKKCCCKKKACCCCAKKCCKKACCCC
DATA AKKKKKKKKKAAAAAACCCCKKCCCCKKCCCCAAAAAAKKKKKKKKKACKKCAAACCAAACKKC
DATA AAKKKKKKKAAAAAAACCCCKKCCCCKKCCCCAAAAAAAKKKKKKKAACCCCAAACCAAACCCC
DATA CCCCCCCCCCCCAAAACCCCKKKCCKKKCCCCAAAACCCCCCCCCCCCAAAAAAACCAAAAAAA
DATA CKCCCCKCCCKCAAAACCCCKKKKKKKKCCCCAAAACKCCCKCCCCKCAAAAAAACCAAAAAAA
DATA CKCCCCKCCCKCAAAACKKCAKKKKKKACKKCAAAACKCCCKCCCCKCAAAAAAACCAAAAAAA
DATA CCCCCCCCCCCCAAAACCCCAAAAAAAACCCCAAAACCCCCCCCCCCCAAAAAAACCAAAAAAA
DATA CCCCCCCCCCCCAAAAAAAAAAACCAAAAAAAAAAACCCCCCCCCCCCCCCCAAAAAAAACCCC
DATA CCCKCCCCKCCCAAAAAAAAAAACCAAAAAAAAAAACCCKCCCCKCCCCCCCAKKKKKKACCCC
DATA CCCKCCCCKCCCAAAAAAAAAAACCAAAAAAAAAAACCCKCCCCKCCCCCCCKKKKKKKKCCCC
DATA CCCCCCCCCCCCAAAAAAAAAAACCAAAAAAAAAAACCCCCCCCCCCCCKKCKKKCCKKKCKKC
DATA AAKKKKKKKAAAAAAACCCCAAACCAAACCCCAAAAAAAKKKKKKKAACCCCKKCCCCKKCCCC
DATA AKKKKKKKKKAAAAAACCCCAAACCAAACCCCAAAAAAKKKKKKKKKACCCCKKCCCCKKCCCC
DATA AKKKCCCKKKAAAAAACCCCAKKCCKKACCCCAAAAAAKKKCCCKKKACCCCKKCCCCKKCCCC
DATA AKKCCCCCCCCCCCCCCKKCKKKCCKKKCKKCCCCCCCCCCCCCCKKACCCCKKKCCKKKCCCC
DATA AKKCCCCCCCCCCCCCCCCCKKKCCKKKCCCCCCCCCCCCCCCCCKKACKKCKKKCCKKKCKKC
DATA AKKKCCCKKKAAAAAACCCCKKCCCCKKCCCCAAAAAAKKKCCCKKKACCCCAKKCCKKACCCC
DATA AKKKKKKKKKAAAAAACCCCKKCCCCKKCCCCAAAAAAKKKKKKKKKACCCCAAACCAAACCCC
DATA AAKKKKKKKAAAAAAACCCCKKCCCCKKCCCCAAAAAAAKKKKKKKAACCCCAAACCAAACCCC
DATA CCCCCCCCCCCCAAAACKKCKKKCCKKKCKKCAAAACCCCCCCCCCCCAAAAAAACCAAAAAAA
DATA CCCKCCCCKCCCAAAACCCCKKKKKKKKCCCCAAAACCCKCCCCKCCCAAAAAAACCAAAAAAA
DATA CCCKCCCCKCCCAAAACCCCAKKKKKKACCCCAAAACCCKCCCCKCCCAAAAAAACCAAAAAAA
DATA CCCCCCCCCCCCAAAACCCCAAAAAAAACCCCAAAACCCCCCCCCCCCAAAAAAACCAAAAAAA

